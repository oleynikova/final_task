//начало текста модуля

///////////////////////////////////////////////////
//Служебные функции и процедуры
///////////////////////////////////////////////////

&НаКлиенте
// контекст фреймворка Vanessa-Behavior
Перем Ванесса;
 
&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;
 
&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-Behavior.
Перем КонтекстСохраняемый Экспорт;

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;
	
	ВсеТесты = Новый Массив;

	//описание параметров
	//Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,Снипет,ИмяПроцедуры,ПредставлениеТеста,Транзакция,Параметр);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ПодготовитьДанныеИзМакетаОбработки(Парам01,Парам02)","ПодготовитьДанныеИзМакетаОбработки","Когда Подготовить данные из макета ""Операторы"" обработки ""ПриемТовараОтПоставщика""");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ЯПодготавливаюДанныеИзМакетаОбработки(Парам01,Парам02)","ЯПодготавливаюДанныеИзМакетаОбработки","И Я подготавливаю данные из макета ""Номенклатура"" обработки ""ПриемТовараОтПоставщика""");

	Возврат ВсеТесты;

КонецФункции
	
&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции
	
&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции



///////////////////////////////////////////////////
//Работа со сценариями
///////////////////////////////////////////////////

&НаКлиенте
// Процедура выполняется перед началом каждого сценария
Процедура ПередНачаломСценария() Экспорт
	
КонецПроцедуры

&НаКлиенте
// Процедура выполняется перед окончанием каждого сценария
Процедура ПередОкончаниемСценария() Экспорт
	
КонецПроцедуры


///////////////////////////////////////////////////
&НаКлиенте
Функция ПолучитьПутьКФайлуОтносительноКаталогоFeatures(ИмяФайлаСРасширением)
	
	ПутьКФайлу = "";
	СостояниеVanessaBehavior = Ванесса.ПолучитьСостояниеVanessaBehavior();
	ПутьКТекущемуFeatureФайлу = СостояниеVanessaBehavior.ТекущаяФича.ПолныйПуть;
	ПутьКФайлу = Лев(ПутьКТекущемуFeatureФайлу, СтрНайти(ПутьКТекущемуFeatureФайлу, "features") - 1) + ИмяФайлаСРасширением;
	Возврат ПутьКФайлу;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПутьКФайлуОбработки(ИмяФайлаОбработки)
	
	ПутьКФайлу = "";
	СостояниеVanessaBehavior = Ванесса.ПолучитьСостояниеVanessaBehavior();
	ПутьКТекущемуFeatureФайлу = СостояниеVanessaBehavior.ТекущаяФича.ПолныйПуть;
	ПутьКФайлу = Лев(ПутьКТекущемуFeatureФайлу, СтрНайти(ПутьКТекущемуFeatureФайлу, "\", НаправлениеПоиска.СКонца) - 1) + "\step_definitions\" + ИмяФайлаОбработки + ".epf";
	Возврат ПутьКФайлу;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьFixtureИзМакета(ИмяМакета, ИмяФайлаОбработки)
	
	Ванесса.ЗапретитьВыполнениеШагов();
	НачальноеИмяФайла = ПолучитьПутьКФайлуОтносительноКаталогоFeatures("tools\Выгрузка и загрузка данных XML.epf");
	Адрес = "";
	СтруктураПараметры = Новый Структура();
	СтруктураПараметры.Вставить("ИмяМакета", ИмяМакета);
	СтруктураПараметры.Вставить("ИмяФайлаОбработки", ИмяФайлаОбработки);
	НачатьПомещениеФайла(
		Новый ОписаниеОповещения("ЗагрузитьFixtureИзМакетаЗавершение", ЭтотОбъект, СтруктураПараметры), 
		Адрес, 
		НачальноеИмяФайла, 
		Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьFixtureИзМакетаЗавершение(УдалосьПоместитьФайл, Адрес, ВыбранноеИмяФайла, Параметры) Экспорт
	
	ПутьКВызываемойОбработки = ПолучитьПутьКФайлуОбработки(Параметры.ИмяФайлаОбработки);
	ЗагрузитьFixtureИзМакетаЗавершениеНаСервере(Адрес, Параметры.ИмяМакета, ПутьКВызываемойОбработки);
	Ванесса.ПродолжитьВыполнениеШагов();

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьFixtureИзМакетаЗавершениеНаСервере(Адрес, ИмяМакета, ПутьКВызываемойОбработки)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяВременногоФайла, Ложь);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	
	ВызываемаяВнешняяОбработка = ВнешниеОбработки.Создать(ПутьКВызываемойОбработки, Ложь);
	Текст = ВызываемаяВнешняяОбработка.ПолучитьМакет(ИмяМакета).ПолучитьТекст();
	
	ВременныйДокумент = Новый ТекстовыйДокумент;
	ВременныйДокумент.УстановитьТекст(Текст);
	ВременныйДокумент.Записать(ИмяВременногоФайла, КодировкаТекста.UTF8);
	
	ВнешняяОбработка.ВыполнитьЗагрузку(ИмяВременногоФайла);

КонецПроцедуры

///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
//Когда Подготовить данные из макета "Операторы" обработки "ПриемТовараОтПоставщика"
//@ПодготовитьДанныеИзМакета(Парам01)
Процедура ПодготовитьДанныеИзМакетаОбработки(ИмяМакета, ИмяОбработки) Экспорт
	ЗагрузитьFixtureИзМакета(ИмяМакета, ИмяОбработки);
КонецПроцедуры

&НаКлиенте
//И Я подготавливаю данные из макета "Номенклатура" обработки "ПриемТовараОтПоставщика"
//@ЯПодготавливаюДанныеИзМакета(Парам01)
Процедура ЯПодготавливаюДанныеИзМакетаОбработки(ИмяМакета, ИмяОбработки) Экспорт
	ЗагрузитьFixtureИзМакета(ИмяМакета, ИмяОбработки)
КонецПроцедуры

//окончание текста модуля