
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина; 
	Движения.ОстаткиТоваров.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Период,
	|	&ВидДвижения,
	|	ПродажаТоваровСписокТоваров.Номенклатура КАК Номенклатура,
	|	СУММА(ПродажаТоваровСписокТоваров.Количество) КАК Количество
	|ПОМЕСТИТЬ ДокТЧ
	|ИЗ
	|	Документ.ПродажаТоваров.СписокТоваров КАК ПродажаТоваровСписокТоваров
	|ГДЕ
	|	ПродажаТоваровСписокТоваров.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажаТоваровСписокТоваров.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокТЧ.Номенклатура,
	|	ДокТЧ.Период,
	|	ДокТЧ.ВидДвижения,
	|	СУММА(ДокТЧ.Количество) КАК Количество
	|ИЗ
	|	ДокТЧ КАК ДокТЧ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТЧ.Номенклатура,
	|	ДокТЧ.Период,
	|	ДокТЧ.ВидДвижения";
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		Движения.ОстаткиТоваров.Записать();
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Движения.ОстаткиТоваров.Загрузить(РезультатЗапроса.Выгрузить());
	
	Движения.Записать();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиТоваровОстатки.Номенклатура,
	|	ОстаткиТоваровОстатки.КоличествоОстаток,
	|	ОстаткиТоваровОстатки.Номенклатура.Представление
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваров.Остатки(
	|			&МоментИтогов,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					ДокТЧ.Номенклатура
	|				ИЗ
	|					ДокТЧ КАК ДокТЧ)) КАК ОстаткиТоваровОстатки
	|ГДЕ
	|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		МоментИтогов = '00010101';
	Иначе
		МоментИтогов = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	КонецЕсли; 
	Запрос.УстановитьПараметр("МоментИтогов", МоментИтогов); 
	
	РезультатПоОстаткам = Запрос.Выполнить();
	
	Если НЕ РезультатПоОстаткам.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатПоОстаткам.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Товара " + Выборка.НоменклатураПредставление + " не хватает: " + (-Выборка.КоличествоОстаток);
			Сообщение.Сообщить(); 
		КонецЦикла;  
	КонецЕсли; 
	
КонецПроцедуры
