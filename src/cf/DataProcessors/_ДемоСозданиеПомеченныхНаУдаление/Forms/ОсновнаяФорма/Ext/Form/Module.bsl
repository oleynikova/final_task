#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитФормыВЗначение("Объект").НастройкиПоУмолчанию());
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьОбъекты(Команда)
	Состояние(НСтр("ru = 'Создание помеченных на удаление'"), , НСтр("ru = 'Пожалуйста, подождите...'"), БиблиотекаКартинок.ДлительнаяОперация48);
	СоздатьОбъектыНаСервере();
	ОповеститьОбИзменении(Тип("СправочникСсылка._ДемоНоменклатура"));
	ПерейтиПоНавигационнойСсылке("e1cib/navigationpoint/Администрирование/Обработка.УдалениеПомеченныхОбъектов.Команда.УдалениеПомеченныхОбъектов");
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьРегламентноеЗадание(Команда)
	ДлительнаяОперация = ДлительнаяОперацияЗапускСервер(УникальныйИдентификатор);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	НастройкиОжидания.Интервал             = 4;
	НастройкиОжидания.ТекстСообщения       = НСтр("ru = 'Удаление помеченных...'");
	
	Обработчик = Новый ОписаниеОповещения("ДлительнаяОперацияЗавершениеКлиент", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
	ПерейтиПоНавигационнойСсылке("e1cib/app/Обработка.ЖурналРегистрации");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьОбъектыНаСервере()
	РеквизитФормыВЗначение("Объект").Сгенерировать(ЭтотОбъект);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДлительнаяОперацияЗапускСервер(УникальныйИдентификатор)
	ИмяМетода = "Обработки._ДемоСозданиеПомеченныхНаУдаление.УдалениеПомеченныхПоРасписанию";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.ОжидатьЗавершение           = 0;
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Удаление помеченных объектов (регламентное)'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, Неопределено, НастройкиЗапуска);
КонецФункции

&НаКлиенте
Процедура ДлительнаяОперацияЗавершениеКлиент(Операция, ДополнительныеПараметры) Экспорт
	Если Операция.Статус = "Выполнено" Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Удаление помеченных успешно завершено'"),,, БиблиотекаКартинок.Успешно32);
	Иначе
		Предупреждение = Новый Структура;
		Предупреждение.Вставить("Текст",    Операция.КраткоеПредставлениеОшибки);
		Предупреждение.Вставить("Подробно", Операция.ПодробноеПредставлениеОшибки);
		СтандартныеПодсистемыКлиент.ВывестиПредупреждение(ЭтотОбъект, Предупреждение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
